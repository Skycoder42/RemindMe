VERSION = 2.2.0

CONFIG += warning_clean exceptions c++14
DEFINES += QT_DEPRECATED_WARNINGS QT_ASCII_CAST_WARNINGS QT_USE_QSTRINGBUILDER

PROJECT_NAME = Syrem
PROJECT_TARGET = syrem
QMAKE_TARGET_COMPANY = "Skycoder42"
QMAKE_TARGET_DESCRIPTION = "Synchronized Reminder App"
QMAKE_TARGET_COPYRIGHT = "Felix Barz"
QMAKE_TARGET_BUNDLE_PREFIX = de.skycoder42

DEFINES += "PROJECT_NAME=\\\"$$PROJECT_NAME\\\""
DEFINES += "PROJECT_TARGET=\\\"$$PROJECT_TARGET\\\""
DEFINES += "VERSION=\\\"$$VERSION\\\""
DEFINES += "COMPANY=\"\\\"$$QMAKE_TARGET_COMPANY\\\"\""
DEFINES += "BUNDLE=\"\\\"$$QMAKE_TARGET_BUNDLE_PREFIX\\\"\""

# enable systemd builds
linux:!android:!no_systemd {
	CONFIG += use_systemd_service
	DEFINES += USE_SYSTEMD_SERVICE
}

# installing
android: INSTALL_TRANSLATIONS = /assets/translations

exists($$PWD/install.pri):include($$PWD/install.pri)
isEmpty(PREFIX) {
	PREFIX = $$[QT_INSTALL_PREFIX]
	isEmpty(INSTALL_BINS): INSTALL_BINS = $$[QT_INSTALL_BINS]
	isEmpty(INSTALL_LIBS): INSTALL_LIBS = $$[QT_INSTALL_LIBS]
	isEmpty(INSTALL_HEADERS): INSTALL_HEADERS = $$[QT_INSTALL_HEADERS]
	isEmpty(INSTALL_PLUGINS): INSTALL_PLUGINS = $$[QT_INSTALL_PLUGINS]
	isEmpty(INSTALL_TRANSLATIONS): INSTALL_TRANSLATIONS = $$[QT_INSTALL_TRANSLATIONS]
} else {
	isEmpty(INSTALL_BINS): INSTALL_BINS = $${PREFIX}/bin
	isEmpty(INSTALL_LIBS): INSTALL_LIBS = $${PREFIX}/lib
	isEmpty(INSTALL_HEADERS): INSTALL_HEADERS = $${PREFIX}/include
	isEmpty(INSTALL_PLUGINS): INSTALL_PLUGINS = $${PREFIX}/plugins
	isEmpty(INSTALL_TRANSLATIONS): INSTALL_TRANSLATIONS = $${PREFIX}/translations
}
isEmpty(INSTALL_SHARE): INSTALL_SHARE = $${PREFIX}/share
